# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           qmake5 1.0

name                qt5-assistant
set module          qttools
version             5.11.2

set branch          [join [lrange [split ${version} .] 0 1] .]

categories          devel aqua
description         A standalone version of Qt's Assistant.
long_description    Qt Assistant is the documentation browser from the QtTools component. \
                    Its full feature set and optimum rendering depends on the (revived) \
                    QtWebKit component.
universal_variant   no
installs_libs       no

maintainers         gmail.com:rjvbertin openmaintainer
platforms           darwin linux
license             LGPL-3

homepage            http://qt.io

subport ${name}-devel {
    description-append This port follows the development branch.
    long_description-append This port follows the development branch.
}

if {${subport} eq "${name}"} {
    distname        ${module}-everywhere-src-${version}
    master_sites    http://download.qt.io/official_releases/qt/${branch}/${version}/submodules
    use_xz          yes
    conflicts-append \
                    ${name}-devel

    checksums       rmd160  4dc542d972d862d20dd8f78768171aeb0f64162d \
                    sha256  f0e8332c771958b4aa373ac643d35e2fabb1a2dcdbaca756ebf3d1ace7c631b8
#                     size    9691816

    livecheck.type  regex
    livecheck.url   http://download.qt.io/archive/qt/${branch}/
    livecheck.regex (\\d+(\\.\\d+)+)
    set PPREFIX     .
} else {
    fetch.type      git
    git.url         git://code.qt.io/qt/${module}.git
    git.branch      1843516c135c8b8cca7bfec9dcf95542329dc0d9
#     v5.12.0-beta1-20-g1843516c (beta=99, beta2=999 etc)
    version         5.11.99.20
    distname        ${module}-git
    conflicts-append \
                    ${name}
    set PPREFIX     devel
}

# a convenience procedure to register conflicts among the different Qt5 ports:
# for instance: [qt5_port_conflicts {qtbase x11}]
# adapted from port:qt5-kde
proc qt5_port_conflicts {components} {
    global available_qt5_versions available_qt_versions name
    set cnfls {}
    if {![info exists available_qt5_versions]} {
        ui_debug "set available_qt5_versions [array get available_qt_versions]"
        set available_qt5_versions [array get available_qt_versions]
    }
    foreach suffix {"" "-devel"} {
        foreach comp ${components} {
            foreach {v info} ${available_qt5_versions} {
                if {"${v}-${comp}" ne "${name}"} {
                    if {${comp} ne "" && ${comp} ne "qtbase"} {
                        set cnfls [lappend cnfls ${v}-${comp}]
                        set cnfls [lappend cnfls ${v}-kde${suffix}-${comp}]
                    } else {
                        set cnfls [lappend cnfls ${v}-qtbase]
                        set cnfls [lappend cnfls ${v}-kde${suffix}]
                    }
                }
            }
        }
    }
    return ${cnfls}
}

options                     extract_components
default extract_components  {}
proc collect_extract_args {} {
    foreach comp [option extract_components] {
        extract.post_args-append ${comp}
    }
}
pre-extract {
    collect_extract_args
}
extract.pre_args-append -T 0
if {${os.platform} ne "darwin"} {
    if {[file exists ${prefix}/bin/bsdtar]} {
        extract.post_args | ${prefix}/bin/bsdtar --no-same-owner -xf -
    }
}

if {[tbool qt5.using_kde]} {
    conflicts-append        [qt5_port_conflicts assistant]
    qt5.depends_component   webkit
} else {
    qt5.depends_component   qtwebkit
}

extract_components-append \
                        ${distname}/src/assistant ${distname}/src/shared

post-extract {
    # install shims so qmake can see the helpng target without
    # having an actual helpng directory
    file delete -force ${worksrcpath}/src/assistant/helpng
    file delete -force ${worksrcpath}/src/assistant/help/helpng.pro
    ln -s help ${worksrcpath}/src/assistant/helpng
    ln -s help.pro ${worksrcpath}/src/assistant/helpng/helpng.pro
}

patch.pre_args          -Np1

patchfiles-append       ${PPREFIX}/patch-qttools-assistant-standalone-build.diff \
                        patch-assistant-fontpanel.diff
# restore support for in-app PDF displaying
patchfiles-append       ${PPREFIX}/patch-restore-pdf-support.diff

default configure.dir   {${workpath}/build}
configure.pre_args-append \
                        ../${worksrcdir}/src/assistant/assistant.pro
default build.dir       {${workpath}/build}
build.target            {}

if {${subport} eq "${name}"} {
    destroot {
        # we bypass the qttools install definitions so we have to do the destroot step ourselves.
        xinstall -m 755 -d ${destroot}${qt_bins_dir}
        if {${os.platform} eq "darwin"} {
            xinstall -m 755 -d ${destroot}${qt_apps_dir}
            file copy ${build.dir}/assistant/QtAssistant.app ${destroot}${qt_apps_dir}/
        } else {
            xinstall -m 755 ${build.dir}/assistant/assistant ${destroot}${qt_bins_dir}
        }
    }
} else {
    # the -devel subport has a more extensive buildsystem patch that adds install targets;
    # it can thus use the normal destroot step.
    post-destroot {
        if {${os.platform} eq "darwin"} {
            xinstall -m 755 -d ${destroot}${qt_apps_dir}
            file rename ${destroot}${qt_bins_dir}/QtAssistant.app ${destroot}${qt_apps_dir}/
            set qtversion [qt5.active_version]
            xinstall -m 755 -d ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/${qtversion}/QtHelpNG/private
            ln -s ${qtversion}/QtHelpNG/private ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/
            ln -s QtHelpNG ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/${qtversion}/QtHelp
            ln -s QtHelpNG ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/QtHelp
            ln -s QtHelpNG ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Versions/5/QtHelp
            file rename {*}[glob -nocomplain ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/*_p.h] \
                ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/${qtversion}/QtHelpNG/private/
            xinstall -m 755 {*}[glob ${filespath}/devel/Q*] ${destroot}${qt_frameworks_dir}/QtHelpNG.framework/Headers/
        }
    }
}

post-destroot {
    if {${os.platform} eq "darwin"} {
        if {[tbool qt5.using_kde]} {
            qt5.add_app_wrapper assistant-qt5 QtAssistant
        }
        file copy ${destroot}${prefix}/bin/assistant-qt5 ${destroot}${qt_bins_dir}/assistant
    } else {
        qt5.add_app_wrapper assistant-qt5 assistant assistant ${qt_bins_dir}
    }
}
