# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4
# $Id: Portfile 127658 2014-10-31 18:21:47Z mcalhoun@macports.org $

PortSystem          1.0

name                qt5-creator

if { ${subport} eq "${name}-docs" || ${subport} eq "${name}-examples" } {
    universal_variant no
    supported_archs   noarch
}

set qt5.prefer_kde  1
PortGroup           qmake5 1.0
# universal build hasn't been tested (20150410)
# universal_variant   no

categories          devel aqua
platforms           darwin
license             GPL-3
maintainers         gmail.com:rjvbertin openmaintainer
description         Cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.
long_description    Qt Creator is a cross-platform integrated development environment (IDE) tailored to the needs of Qt developers.
installs_libs       no

homepage            http://wiki.qt.io/Category:Tools::QtCreator

subport ${name}-mac-devel {
    replaced_by "${name}-devel"
    PortGroup   obsolete 1.0
}
subport ${name}-mac {
    replaced_by "${name}"
    PortGroup   obsolete 1.0
}

variant legacy description {Provides a version that still builds against Qt 5.3.2, and thus on OS X 10.6} {}

# provided by RJVB's qt5-1.0.tcl PortGroup; include a simplified copy here
if {[info procs qt5.depends_component] eq ""} {
    proc qt5.depends_component {first args} {
        global qt5_component_lib
        global qt5.using_kde
        # join ${first} and (the optional) ${args}
        set args [linsert $args[set list {}] 0 ${first}]
        if {[info exists qt5.using_kde] && ${qt5.using_kde}} {
            set qt5main "qt5-kde"
        } else {
            set qt5main "qt5"
        }
        foreach comp ${args} {
            if {${comp} eq "qt5"} {
                if {[info exists qt5.using_kde] && ${qt5.using_kde}} {
                    global qt5_dependency
                    depends_lib-append ${qt5_dependency}
                } else {
                    depends_lib-append port:${qt5main}
                }
            } else {
                set portname "${qt5main}-${comp}"
                if {[info exists qt5_component_lib] && [info exists qt5_component_lib(${comp})]} {
                    # an explicit dependency pattern was given, e.g. path:foo
                    depends_lib-append "$qt5_component_lib(${comp}):${portname}"
                } else {
                    depends_lib-append port:${portname}
                }
            }
        }
    }
}

if {!${qt5.using_kde}} {
    qt5.depends_component \
                        qtscript \
                        qtdeclarative \
                        qttools \
                        qtmacextra \
                        qtquickcontrols \
                        qtsvg
}

subport ${name}-devel {
    conflicts           ${name}
    set use_git         yes
    if {${use_git}} {
        fetch.type      git
        git.url         git://code.qt.io/qt-creator/qt-creator.git
        if {[variant_isset legacy]} {
            git.branch  3a8b52d68cb16aac27be531f4dd55ff816f71081
            # no idea what to set this to ... this commit was between 3.4.1 and 3.4.2 :
            set qbs.branch  760dc4ef7571e780489adcd51ddb80beb000bd21
            version         3.4.2-1.g3a8b52d
        } else {
#             git.branch      25120b2f59d998b5d18c160b653ead5bbc5e603d
#             set qbs.branch  9d28325c404a87757c61b7dba10f54b43c28cd11
#             # v4.0.0-563-g25120b2 / v1.5.0-25-g9d28325 (qbs)
            git.branch      6a061b2a3579b266033599f69e8f3270ff24c426
            set qbs.branch  1b3b13fd65ff8d4741947b06948c2d2ccd6d2a45
            # v4.2.0-beta1-415-g6a061b2 / v1.6.1-114-g1b3b13f
            version         4.2.0.b1.415
            # don't let QtCreator set up a global Mac-style menubar when using the xcb plugin
            patchfiles-append \
                        devel/patch-show-menubar-with-xcb.diff
        }
        distname        qt-creator-opensource-src-git
        fetch {
            # I don't see any provisions for git submodules in MacPorts, so we have to clone by hand:
            if {![file exists ${workpath}/${worksrcdir}]} {
                ui_debug "git clone --recursive ${git.url} ${workpath}/${worksrcdir}"
                system "${prefix}/bin/git clone --recursive -q ${git.url} ${workpath}/${worksrcdir}"
            }
            ui_debug "checking out main branch ${git.branch}"
            system "cd ${workpath}/${worksrcdir} ; ${prefix}/bin/git checkout -q ${git.branch}"
        }

        if {![variant_isset legacy]} {
            # do not opportunistically use QtWebengine
            patchfiles-append \
                        devel/patch-no_qtwebengine.diff
        }

        variant qtwebengine conflicts qtwebkit legacy description {build with QtWebengine support} {
            patchfiles-delete  devel/patch-no_qtwebengine.diff
            qt5.depends_component \
                        qtwebengine
        }

    } else {
#         if {[variant_isset legacy]} {
#             version     3.4.2
#             checksums   rmd160  944ee0982776e580330ad75e9fea0470a33e2b73 \
#                         sha256  f57df9954768badb252154a51cca9b21a4903f8e003ef944d25801cde1754bab
#         } else {
#             version     3.6.1
#             checksums   rmd160  ee20bc80e538e3e8f564b3dd731f85d3c909a858 \
#                         sha256  e53611f80332cd1d171a9b91f8467b6182186cbec6b4a9301f32367368f15de2
#         }
#         master_sites    https://download.qt.io/official_releases/qtcreator/[join [lrange [split ${version} .] 0 1] .]/${version}/
#         distname        qt-creator-opensource-src-${version}
#         livecheck.type  regex
#         livecheck.url   http://www.qt.io/download-open-source/
#         livecheck.regex qt-creator-opensource-src-(\[a-z0-9.\]+)${extract.suffix}
#         # this one will hopefully not be necessary for long
#         patchfiles-append \
#                         patch-qpsprojman.diff \
#                         patch-show-menubar-with-xcb.diff
    }

}
#else
if { ${subport} ne "${name}-devel" } {
    conflicts       ${name}-devel qt5-qtcreator
    # double-check: 3.4.2 should still build against Qt 5.3.2, i.e. on OS X 10.6.8
    if {[variant_isset legacy]} {
        version     3.4.2
        checksums   rmd160  944ee0982776e580330ad75e9fea0470a33e2b73 \
                    sha256  f57df9954768badb252154a51cca9b21a4903f8e003ef944d25801cde1754bab
    } else {
        version     4.1.0
        checksums   rmd160  aad5f1096cf038b23969c6677cb4094515eaaa48 \
                    sha256  412c0223dbfd86983b39e10e2c7cb42b530995cfe2f3aa3529ea3f1b00ffb403
        patchfiles-append \
                    patch-show-menubar-with-xcb-4.1.0.diff
    }
    master_sites    https://download.qt.io/official_releases/qtcreator/[join [lrange [split ${version} .] 0 1] .]/${version}/

    distname        qt-creator-opensource-src-${version}
    livecheck.type  regex
    livecheck.url   http://www.qt.io/download-open-source/
    livecheck.regex qt-creator-opensource-src-(\[a-z0-9.\]+)${extract.suffix}

    # do not opportunistically use QtWebkit
    patchfiles-append      patch-no_qtwebkit.diff

    if {![variant_isset legacy]} {
        # do not opportunistically use QtWebengine
        patchfiles-append  patch-no_qtwebengine.diff
    }

    variant qtwebkit conflicts qtwebengine description {build with QtWebkit support} {
        patchfiles-delete  patch-no_qtwebkit.diff
        qt5.depends_component \
                            qtwebkit
    }
    default_variants        +qtwebkit

    variant qtwebengine conflicts qtwebkit legacy description {build with QtWebengine support} {
        patchfiles-delete  patch-no_qtwebengine.diff
        qt5.depends_component \
                            qtwebengine
    }

}

if {[variant_isset legacy]} {
    patchfiles-append \
                    legacy/patch-no-dockmenu-xcb.diff \
                    legacy/patch-tweak-menus.diff
} else {
    # don't set up a Dock menu when using the xcb plugin
    patchfiles-append \
                    patch-no-dockmenu-xcb.diff \
                    patch-no-icons-in-menus.diff
}

if {${os.platform} eq "darwin"} {
    set appname "Qt Creator.app"
} else {
    set appname qtcreator
}
destroot.destdir    INSTALL_ROOT=${destroot}${prefix}

variant docs description {Include the documentation} {
    build.target-append \
                    docs
    destroot.target-append \
                    install_docs
}

if { ${subport} eq ${name} || ${subport} eq "${name}-devel" } {
    if {${os.platform} eq "darwin"} {
        depends_lib-append     port:botan
    }
    configure.args-append  "USE_SYSTEM_BOTAN=1"
    if {[variant_isset LTO]} {
        configure.args-append   -config ltcg
    }

    post-fetch            {
        file mkdir ${workpath}/build
        if {(${subport} eq "${name}-devel") && ${use_git}} {
            catch {eval exec sh -c "\"cd ${workpath}/${worksrcdir} ; git submodule update --init\""} result
            ui_debug "git submodule -init: $result"
            if {[file exists ${filespath}/QC-git/.git]} {
                reinplace -W ${workpath}/${worksrcdir} "s|qt-labs/qbs.git|files/QC-git/src/shared/qbs|g" .git/config
            }
            catch {eval exec sh -c "\"cd ${workpath}/${worksrcdir} ; git submodule update\""} result
            ui_debug "git submodule: $result"
            ui_debug "checking out QBS branch ${qbs.branch}"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git checkout -q master"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git pull -q"
            system "cd ${workpath}/${worksrcdir}/src/shared/qbs ; ${prefix}/bin/git checkout -q ${qbs.branch}"
        }
    }
    configure.args-append   ../${worksrcdir}/qtcreator.pro
    default configure.dir   {${workpath}/build}
    default build.dir       {${workpath}/build}

    if { ![variant_isset universal] } {
        if {${os.platform} eq "darwin"} {
            destroot {
                # make install attempts to install command line tools into /bin, etc.
                xinstall -m 755 -d ${destroot}${qt_apps_dir}
                copy "${build.dir}/bin/${appname}" "${destroot}${qt_apps_dir}"
                reinplace "s|/usr/local|${prefix}|g" \
                    "${destroot}${qt_apps_dir}/${appname}/Contents/Resources/qbs/share/qbs/imports/qbs/Probes/PathProbe.qbs"
                reinplace "s|/usr/local/lib|${qt_frameworks_dir}|g" \
                    "${destroot}${qt_apps_dir}/${appname}/Contents/Resources/qbs/share/qbs/imports/qbs/Probes/FrameworkProbe.qbs"
          }
        }
    } else {
        # We should really just override destroot, but the muniversal Portgroup would override our override.
        destroot.cmd "true"
        merger-post-destroot {
            foreach arch ${universal_archs_to_use} {
                set adest ${destroot}-${arch}${qt_apps_dir}
                xinstall -m 755 -d ${adest}
                copy "${worksrcpath}-${arch}/bin/${appname}" "${adest}"
                reinplace "s|/usr/local/|${prefix}/|g" \
                    "${adest}/${appname}/Contents/Resources/qbs/share/qbs/imports/qbs/Probes/FrameworkProbe.qbs" \
                    "${adest}/${appname}/Contents/Resources/qbs/share/qbs/imports/qbs/Probes/PathProbe.qbs"
            }
        }
    }
    post-destroot {
        if {[variant_isset docs]} {
            system -W ${build.dir} "${build.cmd} install_docs ${destroot.destdir}"
            platform darwin {
                file delete -force "${destroot}${prefix}/Qt Creator.app"
            }
            xinstall -m 755 -d ${destroot}${qt_docs_dir}
            copy ${build.dir}/doc/html ${destroot}${qt_docs_dir}

        }
        if {${os.platform} eq "darwin"} {
            set QCAppBundle "${qt_apps_dir}/Qt Creator.app"
            xinstall -m 755 -d ${destroot}/${qt_bins_dir}
            xinstall -m 755 ${filespath}/qtcreator ${destroot}/${qt_bins_dir}
            reinplace "s|@@QT_APPS_DIR@@|${qt_apps_dir}|g" ${destroot}/${qt_bins_dir}/qtcreator
            xinstall -m 755 ${filespath}/qtcreator-remote ${destroot}/${qt_bins_dir}
            reinplace "s|@@QT_APPS_DIR@@|${qt_apps_dir}|g" ${destroot}/${qt_bins_dir}/qtcreator-remote
            if {![info exists keep_languages]} {
                if {[file exists ${prefix}/etc/macports/locales.tcl]
                    && [file exists ${destroot}${QCAppBundle}/Contents/Resources/translations]} {
                    if {[catch {source "${prefix}/etc/macports/locales.tcl"} err]} {
                        ui_error "Error reading ${prefix}/etc/macports/locales.tcl: $err"
                        return -code error "Error reading ${prefix}/etc/macports/locales.tcl"
                    }
                }
            }
            if {[info exists keep_languages]} {
                foreach l [glob -nocomplain ${destroot}${QCAppBundle}/Contents/Resources/translations/*.qm] {
                    set langcomps [split [file rootname [file tail ${l}]] _]
                    set simplelang [lindex ${langcomps} end]
                    set complang [join [lrange ${langcomps} end-1 end] _]
                    if {[lsearch -exact ${keep_languages} ${simplelang}] ne "-1"} {
                        ui_debug "won't delete ${l} (${simplelang})"
                    } elseif {[lsearch -exact ${keep_languages} ${complang}] ne "-1"} {
                        ui_debug "won't delete ${l} (${complang})"
                    } else {
                        ui_debug "rm ${l}"
                        file delete -force ${l}
                    }
                }
            }
        }
        qt5.add_app_wrapper qtcreator5 "Qt Creator"
    }
}

subport ${name}-docs {
    replaced_by ${name} +docs
    PortGroup   obsolete 1.0
}

subport ${name}-examples {
    depends_lib-append port:${name}
    build           {}
    destroot.dir    ${worksrcpath}/src/shared/qbs
    destroot.target -f Makefile.static INSTALL_ROOT=${destroot}${prefix} install_examples
}

# kate: backspace-indents true; indent-pasted-text true; indent-width 4; keep-extra-spaces true; remove-trailing-spaces modified; replace-tabs true; replace-tabs-save true; syntax Tcl/Tk; tab-indents true; tab-width 4;
